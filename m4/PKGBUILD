pkgbase='m4'
pkgname=('m4-doc' 'm4-dev' 'm4-lib' 'm4')
pkgver=1.4.18.r2.g10237691
pkgrel=1
pkgdesc='GNU macro processor'
url='https://www.gnu.org/software/m4/m4.html'
arch=('x86_64')
license=('GPL')
depends_doc=()
depends_lib=('musl')
depends_dev=('m4-lib')
depends=('m4-lib')
makedepends=('texinfo' 'rsync')
source=('git://git.sv.gnu.org/m4.git#branch=branch-1.4'
        'git://git.sv.gnu.org/gnulib.git'
        'busybox.patch'
        'disable-tests.patch')
sha256sums=('SKIP'
            'SKIP'
            '344052d315e09e3a042129858349c1d7a27a35d61b09d99173e96f89aaf4131d'
            '569f0db0c15609b17fdd6f0ef7593e5db320efa140c0892db330282f83c6b735')

pkgver() {
  cd "${pkgbase}"
  
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "${pkgbase}"
  
  CFLAGS=${CFLAGS//'-flto=thin'/}
  CXXFLAGS=${CXXFLAGS//'-flto=thin'/}
  LDFLAGS=${LDFLAGS//'-flto=thin'/}
  
  ./bootstrap --skip-git --gnulib-srcdir="${srcdir}/gnulib/"
  
  ./configure --prefix='/usr' \
              --disable-gcc-warnings
  
  make
}

package() {
  package-init
  
  cd "${pkgbase}"
  
  make install DESTDIR="${tmpdir}"
  
  rm "${tmpdir}/usr/lib/charset.alias"
}

package_m4-doc() {
  package-doc
}

package_m4-dev() {
  package-dev
}

package_m4-lib() {
  package-lib
}

package_m4() {
  groups=('base-devel')
  
  package-base
}
